# Fix values
set terminal postscript eps monochrome rounded enhanced font "Helvetica,22"
set key right top
set yrange [0:*]
prefix = 'GeneratedMean'
lineWidth = 6
set border lw lineWidth/2


# Check how many x values there are
file = prefix.'ChunkCompletion.csv'
path = dir.file
stats path using 1 nooutput
t_0 = 600
total_time = STATS_max
total_time_fac = STATS_max / t_0
remaining = floor(STATS_max) % t_0
max_x_range = remaining > t_0/2 ? ceil(STATS_max/t_0) * t_0 : floor(STATS_max/t_0) * t_0 + 0.5 * t_0

# Check how many parts there are
stats path using 2 nooutput
parts = STATS_max * 1.1


# For bandwidth
set yrange [0:1024*22]
set ytics 4096
set xrange[0:max_x_range]

# Make scale render the last point
set xtics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
if(total_time > 1.1 * t_0 && total_time < 1.9 * t_0) {
    set xtics ("T_{0}" t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
}
if(total_time > 2.1 * t_0 && total_time < 2.9 * t_0) {
    set xtics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "3*T_{0}" 3 * t_0)
}

file = prefix.'CurrentUploadBandwidth.csv'
path = dir.file
set ylabel "Upload bandwidth [bytes/seconds]"
set xlabel "Time [seconds]"
set output './img/'.file.'.eps'
plot path using 1:2 with lines lw lineWidth lt 1 title columnheader

file = prefix.'CurrentDownloadBandwidth.csv'
path = dir.file
set ylabel "Download bandwidth [bytes/seconds]"
set xlabel "Time [seconds]"
set output './img/'.file.'.eps'
plot path using 1:2 with lines lw lineWidth lt 1 title columnheader

file = prefix.'CurrentSuperSeederUploadBandwidth.csv'
path = dir.file
set xtics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
set ylabel "Upload bandwidth [bytes/seconds]"
set xlabel "Time [seconds]"
set output './img/'.file.'.eps'
plot path using 1:2 with lines lw lineWidth lt 1 title columnheader





set key left top

# For completion
set yrange [0:parts]
set ytics autofreq
# Make scale render the last point
set xtics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
if(total_time > 1.1 * t_0 && total_time < 1.9 * t_0) {
    set xtics ("T_{0}" t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
}
if(total_time > 2.1 * t_0 && total_time < 2.9 * t_0) {
    set xtics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "3*T_{0}" 3 * t_0)
}

file = prefix.'ChunkCompletion.csv'
path = dir.file
set ylabel "Completion"
set xlabel "Time [seconds]"
set output './img/'.file.'.eps'
plot path using 1:2 with lines lw lineWidth lt 1 title columnheader



set key left bottom

# For completion
set yrange [0:max_x_range]

# Make scale render the last point
set ytics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
if(total_time > 1.1 * t_0 && total_time < 1.9 * t_0) {
    set ytics ("T_{0}" t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "2*T_{0}" 2 * t_0, "3*T_{0}" 3 * t_0)
}
if(total_time > 2.1 * t_0 && total_time < 2.9 * t_0) {
    set ytics ("T_{0}" t_0, "2*T_{0}" 2 * t_0, sprintf("%1.1f*T_{0}", total_time_fac) total_time, "3*T_{0}" 3 * t_0)
}

set xrange [0:*]
set xtics autofreq

# Calc
file = prefix.'SortedChunkCompletion.csv'
path = dir.file
stats path using 1 nooutput

# Calc tics
tics = 8
if (STATS_index_max > 32) {
    tics = 16
}
if(STATS_index_max > 64) {
    tics = 32
}

set xtics floor(tics)
set ylabel "Time [seconds]"
set xlabel "Peers"
set output './img/'.file.'.eps'
plot path using 1:2 with lines lw lineWidth lt 1 title columnheader











